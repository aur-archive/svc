#compdef svc

services() {
	local -a inits xinetds alls
	local expl ret=1

	if chkconfig --list > /dev/null 2>&1; then
		alls=( ${(f)"$(LANGUAGE=C LANG=C LC_ALL=C chkconfig --list)"} )
		inits=( ${${${alls[1,(r)xinetd based*]}[1,-2]}/%[[:space:]]*/} )
		xinetds=( ${${${${alls[(r)xinetd based*,-1]}[2,-1]}/#[[:space:]]#}/%:*} )

	 _alternative \
		'init:init service:compadd -a inits' \
	    'xinetd:xinetd service:compadd -a xinetds' && ret=0
	else
		services_directory=`[ -d /etc/init.d ] && echo /etc/init.d || echo /etc/rc.d`;
		_wanted services expl service compadd "$@" - $services_directory/*(-*:t) && ret=0
	fi

	return ret
}

init_d() {
	local magic cmds what script

	_compskip=all

	script=$words[1]
	[[ $script = */* ]] || script=`[ -d /etc/init.d ] && echo /etc/init.d || echo /etc/rc.d`/$script

	what='(st(art|op|atus)|(force-|)re(start|load)|debug_(up|down)|dump(|_stats)|add|delete|clean|list|config(|ure))'

	read -u0 -k2 magic < $script && [[ $magic = '#!' ]] &&
		cmds=( ${${(j:|:s:|:)${(M)${(f)"$(< $script)"}:#[[:blank:]]#(\'|)${~what}([[:blank:]]#\|[[:blank:]]#${~what})#(\'|)\)}}//[^-a-z_]} )

	(( $#cmds )) || zstyle -a ":completion:${curcontext}:commands" commands cmds ||
		cmds=(start stop)

	_sub_commands $cmds
}

_svc() {
	case $CURRENT in
		2) # Show services under /etc/rc.d
			services
			;;
		3) # Show subcommands for current service

			# This is here so that _init_d works on the service name rather
			# than on the actual function name "svc"
			(( CURRENT-- )) 
			shift words

			# Append enable and disable as service subcommands
			compadd enable disable

			# Append available subcommands for current service
			init_d 
			;;
	esac

}

_svc "$@"
